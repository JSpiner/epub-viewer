// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.2.50'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    configurations {
        ktlint
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.29.0'
        ktlint 'com.github.ridi:ktlint-ruleset:master-SNAPSHOT'
    }

    task ktlint(type: JavaExec, group: 'verification') {
        description = 'Check Kotlin code style.'
        classpath = configurations.ktlint
        main = 'com.github.shyiko.ktlint.Main'
        args 'src/**/*.kt'
    }

    afterEvaluate { project ->
        check.dependsOn ktlint
    }

    task ktlintFormat(type: JavaExec, group: 'formatting') {
        description = 'Fix Kotlin code style deviations.'
        classpath = configurations.ktlint
        main = 'com.github.shyiko.ktlint.Main'
        args '-F', 'src/**/*.kt'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
